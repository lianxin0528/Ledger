apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.didi.dokit'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        multiDexEnabled true
        resConfigs "zh", "en" //保留中文和英文资源
        ndk {
            abiFilters 'armeabi-v7a'//, 'arm64-v8a'
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "proguard-rules.pro"
    }

    // 配置签名
    signingConfigs {
        debug {
            storeFile file(DEBUG_STORE_FILE_LEDGER)
            storePassword DEBUG_STORE_PASSWORD_LEDGER
            keyAlias DEBUG_KEY_ALIAS_LEDGER
            keyPassword DEBUG_KEY_PASSWORD_LEDGER
        }
        release {
            storeFile file(RELEASE_STORE_FILE_LEDGER)
            storePassword RELEASE_STORE_PASSWORD_LEDGER
            keyAlias RELEASE_KEY_ALIAS_LEDGER
            keyPassword RELEASE_KEY_PASSWORD_LEDGER
        }
    }

    buildTypes { // 配置不同版本的build
        release {
            signingConfig signingConfigs.release // 签名
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.debug // 签名
            // 混淆配置My
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    kapt {
        correctErrorTypes = true
    }

    buildFeatures {
        viewBinding = true
    }

}

dokitExt {
    // 通用设置
    comm {
        gpsSwitch true // 地图经纬度开关
        networkSwitch true // 网络开关
        bigImgSwitch true // 大图开关
        webViewSwitch true // webView js 抓包
    }

    slowMethod {
        // 调用栈模式配置
        stackMethod {
            thresholdTime 10 // 默认值为 5ms 小于该值的函数在调用栈中不显示
            enterMethods = ["com.didichuxing.doraemondemo.MainDebugActivity.test1"] // 调用栈函数入口
            methodBlacklist = ["com.facebook.drawee.backends.pipeline.Fresco"] // 黑名单 粒度最小到类 暂不支持到方法
        }
        // 普通模式配置
        normalMethod {
            thresholdTime 500 // 默认值为 500ms 小于该值的函数在运行时不会在控制台中被打印
            packageNames = ["com.didichuxing.doraemondemo"] // 需要针对函数插装的包名
            methodBlacklist = ["com.didichuxing.doraemondemo.dokit"] // 不需要针对函数插装的包名&类名
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation project(':mine')

    kapt 'com.airbnb.android:epoxy-processor:3.8.0' // expoxy
    kapt 'com.alibaba:arouter-compiler:1.2.2' // ARouter

    // 哆啦A梦调试工具
    debugImplementation('com.didichuxing.doraemonkit:dokitx:3.3.5') {
        exclude group: 'com.blankj'
    }
    releaseImplementation('com.didichuxing.doraemonkit:dokitx-no-op:3.3.5') {
        exclude group: 'com.blankj'
    }

}

// 打包相关
android.applicationVariants.all { variant ->
    variant.outputs.all { output ->
        String appName = "" // App名称
        String path = getProjectDir().getPath() + "/src/main/res/values/strings.xml"
        new XmlSlurper().parse(path)."string".find { name ->
            if (name."@name" == "app_name") {
                appName = name
                return true
            }
        }

        // Debug还是Release
        String typeName = buildType.name
        typeName = typeName.substring(0, 1).toUpperCase() + typeName.substring(1).toLowerCase()

        String versionName = String.valueOf(rootProject.ext.android.versionName) // versionName
        String versionCode = String.valueOf(rootProject.ext.android.versionCode) // versionCode
        String buildTime = String.valueOf(rootProject.ext.android.buildTime) // 打包日期
        String suffix = ".apk" // 后缀名
        output.outputFileName = "${appName}_${typeName}_${versionName}_${versionCode}_${buildTime}${suffix}"
    }
}
